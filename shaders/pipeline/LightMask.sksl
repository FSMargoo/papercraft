//
// LightMask.sksl (Written in non-standard SKSL):
//      The light mask shader in the papercraft pipeline,
//      it will generating the light mask image for the
//      next rendering 
//

// The uniform varaibles

// The light type of the shader
//      If LightType < 10. = Ellipse
//      Else = Rectangle
uniform float LightType;
// The X position of the light
uniform float2 Center;
// The radius of the light source object
uniform float Radius;
// The range of the light source
uniform float Range;
// The light brighteness level of the light source
uniform float Level;
// The color of the light source
uniform float3 Color;

// The texture shader for background texture
uniform shader BackgroundTexture;

// The ray structure
struct Ray {
    vec2 Origin;
    vec2 Direction;
};
// The light source object structure
struct LObject {
    vec2    Center;
    vec3    Color;
    float   Radius;
    float   ValidRadius;
    float   Intensity;
};

// The SDF of ellipse
float EllipseSDF(in Ray R, in float Radius) {
    vec2 ray = R.Origin - R.Direction;
    return length(ray) - Radius;
}
// The SDF of rectangle
float RectangleSDF(in Ray R, in float Radius) {
    vec2 Point  = R.Direction - R.Origin;
    vec2 Box    = vec2(Radius, Radius);
    vec2 Delta  = abs(Point) - Box;
    return length(max(Delta, vec2(0.))) + min(max(Delta.x, Delta.y), 0.0);
}
// The phi interpolation function
float Phi(in float Value) {
    return 1.0 - pow(Value, 0.3);
}

// Calculate the light brightness contribution from the SDF
vec3 SDFContribution(in float Distance, in LObject Object) {
    vec3 col = vec3(0., 0., 0.);
    if (Distance <= 0.0) {
        float D = Object.Intensity + 1.;
        col = Object.Color * D;
    }
    else if (Distance <= Object.ValidRadius) {
        float F = (Distance / Object.ValidRadius);
        float D = Phi(F) * (Object.Intensity + 1.);
        col = vec3(Object.Color[0] * D, Object.Color[1] * D, Object.Color[2] * D);
    }

    return col;
}
// Sample the coord light from a ellipse object
vec3 SampleEllipse(in LObject Object, in vec2 Coord) {
    Ray R;
    R.Origin    = Object.Center;
    R.Direction = Coord;

    float sdf = EllipseSDF(R, Object.Radius);
    return SDFContribution(sdf, Object);
}
// Sample the coord light from a rectangle object
vec3 SampleRectangle(in LObject Object, in vec2 Coord) {
    Ray R;
    R.Origin    = Object.Center;
    R.Direction = Coord;

    float sdf = RectangleSDF(R, Object.Radius);
    return SDFContribution(sdf, Object);
}

// Fix the gamma value
vec3 GammaFixed(in vec3 R) {
    return vec3(pow(R[0], 0.9), pow(R[1], 0.9), pow(R[2], 0.9));
}

vec4 main(in vec2 fragCoord) {
    LObject object;
    object.Center       = Center;
    object.Color        = Color;
    object.Radius       = Radius;
    object.ValidRadius  = Range;
    object.Intensity    = Level;

	vec4 fragColor = BackgroundTexture.eval(fragCoord);
    if (LightType < 10.) fragColor += vec4(SampleEllipse(object, fragCoord), 1.);
    else fragColor += vec4(SampleRectangle(object, fragCoord), 1.);

	return fragColor;
}